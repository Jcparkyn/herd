
mandelbrot = \size\ (
  var sum = 0;
  var byteAcc = 0;
  var bitNum = 0;
  for y in range 0 size do (
    ci = (2 * y / size) - 1;
    for x in range 0 size do (
      var zrzr = 0;
      var zizi = 0;
      var zi = 0;
      cr = (2.0 * x / size) - 1.5;

      var z = 0;
      var done = false;
      var escape = 0;
      while not done and z < 50 do (
        zr = zrzr - zizi + cr;
        set zi = 2.0 * zr * zi + ci;

        // preserve recalculation
        set zrzr = zr * zr;
        set zizi = zi * zi;
        if zrzr + zizi > 4.0 then (
            set done = true;
            set escape = 1;
        )
        set z = z + 1;
      )

      set byteAcc = (byteAcc | shiftLeft 1) + escape;
      set bitNum = bitNum + 1;

      if bitNum == 8 then (
        set sum = xor sum byteAcc;
        set byteAcc = 0;
        set bitNum  = 0;
      ) else (
        if x == size - 1 then (
          set byteAcc |= shiftLeft (8 - bitNum);
          set sum = xor sum byteAcc;
          set byteAcc = 0;
          set bitNum  = 0;
        )
      )
    )
  )

  sum
);

main = \\ (
  println (mandelbrot 1);
  println (mandelbrot 500);
  println (mandelbrot 750);
  println (mandelbrot 3000);
);

main ();
