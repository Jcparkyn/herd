use std::{str::FromStr};
use crate::pos::{Span, Spanned};
use crate::ast;
use crate::rc::Rc;
use crate::ast::{Expr, Opcode, Statement, Block, AssignmentTarget, VarRef, LambdaExpr, MatchPattern, MatchExpr};
use crate::parse_helpers::{
    parse_string_literal,
    process_list_match,
    process_declaration,
    make_implicit_lambda,
    assignment_target_to_index_expr,
};

grammar;

match {
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    _
}

pub Program: Vec<ast::SpannedStatement> = {
    SpStatement+
}


// The last line in a REPL input can be an expression, which we automatically convert
// to a return statement.
pub ReplProgram: Vec<ast::SpannedStatement> = {
    // separate patterns to remove ambiguity.
    // If there's no trailing semicolon it should always be an Expr.
    <mut s:SpStatement*> <e:SpExpr> => {
        s.push(e.span.wrap(Statement::Return(e)));
        s
    },
    <mut s1:SpStatement*> <s2:Sp<StatementWithSemicolon>> => {
        s1.push(s2);
        s1
    },
}

Statement: Statement = {
    StatementWithSemicolon,
    Sp<ExprWithBlock> => Statement::Expression(<>),
}

StatementWithSemicolon: Statement = {
    <e:Sp<ExprWithoutBlock>> ";" => Statement::Expression(e),
    <decl_type:DeclarationType> <name:Sp<Identifier>> "=" <e:SpExpr> ";" =>
        process_declaration(name, decl_type, e),
    "set" <target:Sp<AssignmentTarget>> "=" <e:SpExpr> ";" =>
        Statement::PatternAssignment(target.map(MatchPattern::Assignment), e),
    "set" <target:Sp<AssignmentTarget>> "|" "=" <rhs_l:@L> <callee:Sp<Term>> <mut args:Sp<Term>*> <rhs_r:@R> ";" => {
        args.insert(0, assignment_target_to_index_expr(&target));
        let rhs_span = Span::new(rhs_l, rhs_r);
        let rhs = rhs_span.wrap(Expr::Call { callee: Box::new(callee), args });
        Statement::PatternAssignment(target.map(MatchPattern::Assignment), rhs)
    },
    "!" <pattern:Sp<MatchPattern>> "=" <e:SpExpr> ";" =>
        Statement::PatternAssignment(pattern, e),
    "return" <e:SpExpr> ";" => Statement::Return(e),
    <span:Sp<"return">> ";" => Statement::Return(span.span.wrap(Expr::Nil)),
}

Expr = {
    ExprWithoutBlock,
    ExprWithBlock,
}

ExprWithoutBlock = {
    LogicExpr,
    PipeExpr,
    LambdaExpr,
}

// Expressions that don't need a trailing semicolon
ExprWithBlock: Expr = {
    IfExpr,
    MatchExpr,
    "for" <var:Sp<MatchPattern>> "in" <iter:Box<SpExpr>> "do" <body:Box<Sp<Term>>> =>
        Expr::ForIn { var, iter, body },
    "while" <condition:Box<SpExpr>> "do" <body:Box<Sp<Term>>> =>
        Expr::While { condition, body },
    // No blocks here at the moment, because they conflict with using block expression as terms
    // <BlockExpr> => Box::new(Expr::Block(<>)),
}

LogicExpr = Tier<LogicOp, ComparisonExpr>;

ComparisonExpr = Tier<ComparisonOp, AddSubCallExpr>;

AddSubCallExpr: Expr = {
    <callee:Box<Sp<Term>>> <args:Sp<Term>+> => Expr::Call { callee, args },
    "import" <path:StringLiteral> => Expr::Import { path },
    "BUILTIN" ":" <callee:Identifier> <args:Sp<Term>*> => Expr::CallBuiltin {
        callee,
        args,
    },
    AddSubExpr,
}

AddSubExpr = Tier<AddSubOp, Factor>;

Factor = Tier<FactorOp, Term>;

Term: Expr = {
    NumLiteral => Expr::Number(<>),
    BoolLiteral => Expr::Bool(<>),
    StringLiteral => Expr::from_string(<>),
    Identifier => Expr::Variable(VarRef::new(<>)),
    ImplicitLambdaExpr,
    DictLiteral,
    ListLiteral,
    BlockExpr,
    <lhs:Sp<Term>> <index:Sp<ValueIndex>> => Expr::GetIndex(Box::new(lhs), Box::new(index)),
};

PipeLhs: Expr = {
    PipeExpr,
    LogicExpr,
    IfExpr,
    MatchExpr,
}

PipeExpr: Expr =
    <lhs:Sp<PipeLhs>> "|" <callee:Sp<Term>> <mut args:Sp<Term>*> => {
        args.insert(0, lhs);
        Expr::Call { callee: Box::new(callee), args }
    };

ValueIndex: Expr = {
    "." <field:Identifier> => Expr::from_string(field),
    ".[" <index:Expr> "]" => index,
} 

AssignmentTarget: AssignmentTarget = {
    <var:Identifier> <path:Sp<ValueIndex>*> => AssignmentTarget { var: VarRef::new(var), path },
}

MatchPattern: MatchPattern = {
    <decl_type:DeclarationType> <var:Identifier> => {
        if var == "_" {
            MatchPattern::Discard
        } else {
            MatchPattern::Declaration(VarRef::new(var), decl_type)
        }
    },
    "set" <target:AssignmentTarget> => MatchPattern::Assignment(target),
    "[" <parts:Comma<MatchListPart>> "]" =>? Ok(process_list_match(parts)?),
    "{" <entries:Comma<MatchDictPart>> "}" => MatchPattern::Dict(ast::DictPattern { entries }),
    NumLiteral => MatchPattern::Constant(ast::MatchConstant::Number(<>)),
    BoolLiteral => MatchPattern::Constant(ast::MatchConstant::Bool(<>)),
    StringLiteral => MatchPattern::Constant(ast::MatchConstant::String(<>)),
    "(" ")" => MatchPattern::Constant(ast::MatchConstant::Nil),
}

#[inline]
DeclarationType: ast::DeclarationType = {
    "var" => ast::DeclarationType::Mutable,
    () => ast::DeclarationType::Const,
}

MatchListPart: (MatchPattern, bool) = {
    <spread:".."?> <pattern:MatchPattern> => (pattern, spread != None),
    ".." => (MatchPattern::Discard, true),
}

MatchDictPart: (String, MatchPattern) = {
    <key:Identifier> ":" <pattern:MatchPattern> => (key, pattern),
    <decl_type:DeclarationType> <var:Identifier> => {
        let key = var.clone();
        let val = MatchPattern::Declaration(VarRef::new(var), decl_type);
        (key, val)
    },
}

ElseClause: Expr = {
    Term,
    IfExpr,
}

IfExpr: Expr =
    "if " <cond:SpExpr> "then" <b1:Sp<Term>> <b2:("else" <Box<Sp<ElseClause>>>)?> =>
        Expr::If {
            condition: Box::new(cond),
            then_branch: Box::new(b1),
            else_branch: b2,
        };

MatchExpr: Expr =
    "switch" <condition:SpExpr> "on" "{" <branches:CommaNonEmpty<MatchExprBranch>> "}" =>
        Expr::Match(Box::new(MatchExpr { condition, branches }));

MatchExprBranch: (Spanned<MatchPattern>, ast::SpannedExpr) =
    <p:Sp<MatchPattern>> "=>" <e:SpExpr> => (p, e);

BlockExpr: Expr = {
    "(" <b:BlockBodyNonEmpty> ")" => Expr::Block(b),
    "(" ")" => Expr::Nil,
}

BlockBodyNonEmpty: Block = {
    // separate patterns to remove ambiguity.
    // If there's no trailing semicolon it should always be an Expr.
    <s:SpStatement*> <e:SpExpr> => Block { statements: s, expression: Some(Box::new(e)) },
    <mut s1:SpStatement*> <s2:Sp<StatementWithSemicolon>> => Block {
        statements: { s1.push(s2); s1 },
        expression: None,
    },
}

NumLiteral: f64 = {
    r"-?[0-9]+(\.[0-9]+)?" => f64::from_str(<>).unwrap()
};

BoolLiteral: bool = {
    "true" => true,
    "false" => false,
};

StringLiteral: String = {
    r#"'([^']|\\[\\nt'"])*'"# => parse_string_literal(<>),
}

LambdaExpr: Expr = {
    "\\" <params:Sp<MatchPattern>+> "\\" <body:SpExpr> => Expr::Lambda(
        LambdaExpr::new(params, Rc::new(body))
    ),
    "\\" <span:Sp<()>> "\\" <body:SpExpr> => {
        let pattern = span.map(|_| MatchPattern::NIL);
        Expr::Lambda(
            LambdaExpr::new(vec![pattern], Rc::new(body))
        )
    },
}

ImplicitLambdaExpr: Expr =
    "\\" "(" <body:Sp<BlockBodyNonEmpty>> ")" => make_implicit_lambda(body);

DictEntry: (ast::SpannedExpr, ast::SpannedExpr) = {
    <key:SpIdentifier> ":" <val:SpExpr> => (key, val),
    "[" <key:SpExpr> "]" ":" <val:SpExpr> => (key, val),
    <keyval:Sp<Identifier>> => {
        let key = keyval.clone().map(Expr::from_string);
        let val = keyval.map(VarRef::new).map(Expr::Variable);
        (key, val)
    },
}

DictLiteral: Expr = {
    "{" <entries:Comma<DictEntry>> "}" => Expr::Dict(entries),
}

ListLiteral: Expr = {
    "[" <elements:Comma<SpExpr>> "]" => Expr::List(elements),
}

Identifier: String = {
    <r"[a-zA-Z]\w*"> => <>.to_string(),
    "_" => <>.to_string(),
}

SpIdentifier: ast::SpannedExpr = Sp<Identifier> => <>.map(Expr::from_string);

LogicOp: Opcode = {
    "and" => Opcode::And,
    "or" => Opcode::Or,
}

ComparisonOp: Opcode = {
    ">" => Opcode::Gt,
    ">=" => Opcode::Gte,
    "<" => Opcode::Lt,
    "<=" => Opcode::Lte,
    "==" => Opcode::Eq,
    "!=" => Opcode::Neq,
}

AddSubOp: Opcode = {
    "+" => Opcode::Add,
    "++" => Opcode::Concat,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Tier<Op,NextTier>: Expr = {
    <lhs:Sp<Tier<Op,NextTier>>> <op:Op> <rhs:Sp<NextTier>> =>
        Expr::Op { op, lhs: Box::new(lhs), rhs: Box::new(rhs) },
    NextTier
};

Sp<Rule>: Spanned<Rule> =
    <l: @L> <rule: Rule> <r: @R> => Spanned::new(
        Span::new(l, r),
        rule,
    );

SpExpr = Sp<Expr>;

SpStatement: ast::SpannedStatement = Sp<Statement>;

Box<T>: Box<T> = T => Box::new(<>);

#[inline]
CommaNonEmpty<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:(<T> ","?)> => {
        v.push(e);
        v
    }
};

#[inline]
Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}
