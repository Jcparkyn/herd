use std::{str::FromStr,rc::Rc};
use crate::ast::{Expr, Opcode, Statement, Block, AssignmentTarget};
use crate::parse_helpers::parse_string_literal;

grammar;

pub Program: Vec<Statement> = {
    Statement+
}

pub Statement: Statement = {
    StatementWithSemicolon,
    <ExprWithBlock> => Statement::Expression(<>),
}

StatementWithSemicolon: Statement = {
    <ExprWithoutBlock> ";" => Statement::Expression(<>),
    <name:Identifier> "=" <e:Expr> ";" => Statement::Declaration(name, e),
    "set" <target:AssignmentTarget> "=" <e:Expr> ";" => Statement::Assignment(target, e),
    "return" <e:Expr> ";" => Statement::Return(e),
}

ValueIndex: Box<Expr> = {
    "." <field:Identifier> => Box::new(Expr::String(field)),
    ".[" <index:Expr> "]" => index,
} 

AssignmentTarget: AssignmentTarget = {
    <var:Identifier> <path:ValueIndex*> => AssignmentTarget { var, path },
}   

ExprWithoutBlock = LogicExpr;
ExprWithBlock: Box<Expr> = {
    IfExpression,
    "for" <var:Identifier> "in" <iter:Expr> "do" <body:BlockExpr> => Box::new(Expr::ForIn { var, iter, body }),
    <BlockExpr> => Box::new(Expr::Block(<>)),
}

IfExpression: Box<Expr> = {
    "if " <cond:Expr> "then" <b1:BlockExpr> <b2:("else" <BlockExpr>)?> => Box::new(
        Expr::If { condition: cond, then_branch: b1, else_branch: b2 }
    )
}

BlockExpr: Block = {
    "{" <b:BlockBody> "}" => b
}

BlockBody: Block = {
    => Block { statements: vec![], expression: None },
    // separate patterns to remove ambiguity.
    // If there's no trailing semicolon it should always be an Expr.
    <s:Statement*> <e:Expr> => Block { statements: s, expression: Some(e) },
    <mut s1:Statement*> <s2:StatementWithSemicolon> => Block {
        statements: { s1.push(s2); s1 },
        expression: None,
    },
}

NumLiteral: f64 = {
    r"-?[0-9]+" => f64::from_str(<>).unwrap()
};

BoolLiteral: bool = {
    "true" => true,
    "false" => false,
};

LambdaExpr: Box<Expr> = {
    "{" <params:Comma<Identifier>> "->" <body:BlockBody> "}" => Box::new(Expr::Lambda { params, body: Rc::new(body), potential_captures: vec![] })
}

DictEntry: (String, Box<Expr>) = {
    <key:Identifier> ":" <val:Expr> => (key, val),
}

DictLiteral: Box<Expr> = {
    "[" <entries:CommaPlus<DictEntry>> "]" => Box::new(Expr::Dict(entries)),
    "[:]" => Box::new(Expr::Dict(vec![])),
}

ArrayLiteral: Box<Expr> = {
    "[" <elements:Comma<Expr>> "]" => Box::new(Expr::Array(elements)),
}

Identifier: String = {
    <r"[a-zA-Z]\w*"> => <>.to_string(),
}

Term: Box<Expr> = {
    NumLiteral => Box::new(Expr::Number(<>)),
    BoolLiteral => Box::new(Expr::Bool(<>)),
    "nil" => Box::new(Expr::Nil),
    r"'[^']*'" => Box::new(Expr::String(parse_string_literal(<>))),
    Identifier => Box::new(Expr::Variable { name: <>, is_final: true }),
    "(" <Expr> ")",
    LambdaExpr,
    DictLiteral,
    ArrayLiteral,
    <lhs:Term> <index:ValueIndex> => Box::new(Expr::GetIndex(lhs, index)),
};

Tier<Op,NextTier>: Box<Expr> = {
    <lhs:Tier<Op,NextTier>> <op:Op> <rhs:NextTier> => Box::new(Expr::Op { op, lhs, rhs }),
    NextTier
};

CallExpr: Box<Expr> = {
    <callee:Term> <args:Term+> => Box::new(Expr::Call { callee, args }),
}

pub Expr = {
    ExprWithoutBlock,
    ExprWithBlock,
}

LogicExpr = Tier<LogicOp, ComparisonExpr>;
ComparisonExpr = Tier<ComparisonOp, AddSubCallExpr>;
AddSubCallExpr: Box<Expr> = {
    <lhs:AddSubCallExpr> <op:AddSubOp> <rhs:Factor> => Box::new(Expr::Op { op, lhs, rhs }),
    Factor,
    CallExpr,
}
Factor = Tier<FactorOp, Term>;

LogicOp: Opcode = {
    "and" => Opcode::And,
    "or" => Opcode::Or,
}

ComparisonOp: Opcode = {
    ">" => Opcode::Gt,
    "<" => Opcode::Lt,
    "==" => Opcode::Eq,
    "!=" => Opcode::Neq,
}

AddSubOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

CommaPlus<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

Comma<T>: Vec<T> = {
    CommaPlus<T>,
    => vec![],
}