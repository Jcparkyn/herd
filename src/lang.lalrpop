use std::str::FromStr;
use crate::ast::{Expr, Opcode, Statement, Block};

grammar;

pub Statement: Statement = {
    "print" <e:Expr> ";" => Statement::Print(e),
    <Expr> ";" => Statement::Expression(<>),
    <name:Identifier> "=" <e:Expr> ";" => Statement::Declaration(name, e),
    <name:Identifier> ":=" <e:Expr> ";" => Statement::Assignment(name, e),
    "if " <cond: Expr> <b1:BlockExpression> "else" <b2:BlockExpression> => Statement::Expression(
        Box::new(
            Expr::If { condition: cond, then_branch: b1, else_branch: Some(b2) }
        )
    )
}

BlockExpression: Block = {
    "{" <s:Statement*> "}" => Block { statements: s },
}

NumLiteral: f64 = {
    r"-?[0-9]+" => f64::from_str(<>).unwrap()
};

BoolLiteral: bool = {
    "true" => true,
    "false" => false,
};

Identifier: String = {
    <r"[a-zA-Z]\w*"> => <>.to_string(),
}

Term: Box<Expr> = {
    NumLiteral => Box::new(Expr::Number(<>)),
    BoolLiteral => Box::new(Expr::Bool(<>)),
    "nil" => Box::new(Expr::Nil),
    Identifier => Box::new(Expr::Variable(<>)),
    "(" <Expr> ")",
    <BlockExpression> => Box::new(Expr::Block(<>)),
};

pub Expr: Box<Expr> = {
    <lhs:Expr> <op:ExprOp> <rhs:Factor> => Box::new(Expr::Op { op, lhs, rhs }),
    Factor,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
    ">" => Opcode::Gt,
    "<" => Opcode::Lt,
    "==" => Opcode::Eq,
    "!=" => Opcode::Neq,
};

Factor: Box<Expr> = {
    <lhs:Factor> <op:FactorOp> <rhs:Term> => Box::new(Expr::Op { op, lhs, rhs }),
    Term,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};