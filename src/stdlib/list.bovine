
!{ range } = import '@basics';
!{ floor } = import '@math';

push = \arr element\ NATIVE:push arr element;

pop = \arr\ NATIVE:pop arr;

len = \val\ NATIVE:len val;

sort = \arr\ NATIVE:sort arr;

map = \var arr f\ (
  l = len arr;
  for i in range 0 l do (
    set arr.[i] = f arr.[i];
  )
  arr
);

filter = \arr f\ (
  var result = [];
  for item in arr do (
    if f item then (
      set result |= push item;
    )
  )
  result
);

reverse = \var arr\ (
  l = len arr;
  imax = floor (l / 2);
  for i in range 0 imax do (
    i2 = l - 1 - i;
    tmp = arr.[i];
    set arr.[i] = arr.[i2];
    set arr.[i2] = tmp;
  )
  arr
);

return {
  push,
  pop,
  len,
  sort,
  map,
  filter,
  reverse,
};
