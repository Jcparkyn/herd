
!{ range, min } = import '@basics';
!{ floor, clamp } = import '@math';

push = \arr element\ BUILTIN:push arr element;

pop = \arr\ BUILTIN:pop arr;

len = \val\ BUILTIN:len val;

// sort = \arr\ BUILTIN:sort arr;

map = \var arr f\ (
  l = len arr;
  for i in range 0 l do (
    set arr.[i] = f arr.[i];
  )
  arr
);

filter = \arr f\ (
  var result = [];
  for item in arr do (
    if f item then (
      set result |= push item;
    )
  )
  result
);

reverse = \var arr\ (
  l = len arr;
  imax = floor (l / 2);
  for i in range 0 imax do (
    i2 = l - 1 - i;
    tmp = arr.[i];
    set arr.[i] = arr.[i2];
    set arr.[i2] = tmp;
  )
  arr
);

slice = \arr start end\ (
  var result = [];
  actualEnd = end | clamp 0 (len arr);
  actualStart = start | clamp 0 (len arr);
  for i in range actualStart actualEnd do (
    set result |= push arr.[i];
  )
  result
);

return {
  push,
  pop,
  len,
  // sort,
  map,
  filter,
  reverse,
  slice,
};
